# == Class: dirsrv
#
# This class installs and configures 389 Directory Server. Support for 
# post-install configuration management is very limited at this point.
#
# Many of the class parameters define the contents of the silent-install.inf 
# file. For details on the parameters look here:
#
# <https://access.redhat.com/documentation/en-US/Red_Hat_Directory_Server/9.0/html/Installation_Guide/Advanced_Configuration-Silent.html>
#
# Note that the the setup-ds-admin script is picky about reverse DNS lookups and 
# that you may have to setup a static entry in /etc/hosts to work around it in 
# some environments:
#
# <https://access.redhat.com/documentation/en-US/Red_Hat_Directory_Server/9.0/html/Installation_Guide/Preparing_for_a_Directory_Server_Installation-Considerations.html#Preparing_for_a_Directory_Server_Installation-Port_Number>
#
# Currently this module has some limitations:
#
# - Creating or monitoring additional Directory Server instances is not 
#   supported
# - Creating (separate) Config Directory Server instances is not supported
# - Updating the Directory/Admin Server configuration is not supported
#
# == Parameters
#
# [*manage_config*]
#   Whether or not to manage LDAP configuration. Valid values 'yes' and 'no'. 
#   Defaults to 'no'.
# [*serveridentifier*]
#   Identifier for the Directory Server instance. Defaults to $::hostname.
# [*ldap_port*]
#   Directory Server (LDAP) port. Defaults to 389.
# [*suffix*]
#   Directory tree suffix. For example "dc=domain,dc=com". No default value.
# [*rootdn*]
#   Root DN (i.e. "Directory Manager") for the Directory Server. Defaults to 
#   'cn=Directory Manager'.
# [*rootdn_passwd*]
#   Password for the Root DN user.
# [*config_directory_ldap_url*]
#   LDAP URL of the Config Directory. The Config Directory will get generated by 
#   the setup-ds-admin script. The default value 
#   ('ldap://localhost:389/o=NetscapeRoot') should not be changed until this 
#   module supports adding new Directory Server instances that don't 
#   (necessarily) have their own local Config Directory server.
# [*config_directory_admin_id*]
#   Admin user for the Configuration Directory. Defaults to 'admin'.
# [*config_directory_admin_pwd*]
#   Password for the Configuration Directory admin user. No default value.
# [*admin_port*]
#   The port on which the Admin Server listens. Defaults to 9830.
# [*server_admin_id*]
#   Admin username for the Admin Server. Defaults to 'admin'.
# [*server_admin_pwd*]
#   Password for the Admin Server admin user. No default value.
# [*dirsrv_allow_ipv4_address*]
#   IPv4 address/subnet from which to allow connections to the Directory Server. 
#   Use 'any' for any address. Defaults to '127.0.0.1'.
# [*dirsrv_allow_ipv6_address*]
#   IPv6 address/subnet from which to allow connections to the Directory Server. 
#   Use 'any' for any address. Defaults to '::1'.
# [*admin_srv_allow_ipv4_address*]
#   IPv4 address/subnet from which to allow connections to the Admin Server. 
#   Use 'any' for any address. Defaults to '127.0.0.1'.
# [*admin_srv_allow_ipv6_address*]
#   IPv6 address/subnet from which to allow connections to the Admin Server. 
#   Use 'any' for any address. Defaults to '::1'.
# [*monitor_email*]
#   Email address where local service monitoring software sends it's reports to.
#   Defaults to global variable $::servermonitor.
#
# == Authors
#
# Samuli Seppänen <samuli.seppanen@gmail.com>
#
# Samuli Seppänen <samuli@openvpn.net>
#
# == License
#
# BSD-license. See file LICENSE for details.
#
class dirsrv
(
    $manage_config = 'no',
    $serveridentifier = $::hostname,
    $ldap_port = 389,
    $suffix,
    $rootdn = 'cn=Directory Manager',
    $rootdn_pwd,
    $config_directory_ldap_url = "ldap://localhost:389/o=NetscapeRoot",
    $config_directory_admin_id = 'admin',
    $config_directory_admin_pwd,
    $admin_bind_ip = '',
    $admin_port = 9830,
    $server_admin_id = 'admin',
    $server_admin_pwd,
    $dirsrv_allow_ipv4_address = '127.0.0.1',
    $dirsrv_allow_ipv6_address = '::1',
    $admin_srv_allow_ipv4_address = '127.0.0.1',
    $admin_srv_allow_ipv6_address = '::1',
    $monitor_email = $::servermonitor

) inherits dirsrv::params
{
# Rationale for this is explained in init.pp of the sshd module
if hiera('manage_dirsrv', 'true') != 'false' {

    include dirsrv::install

    if $manage_config == 'yes' {

        class { 'dirsrv::config':
            serveridentifier => $serveridentifier,
            ldap_proto => $ldap_proto,
            ldap_port => $ldap_port,
            suffix => $suffix,
            rootdn => $rootdn,
            rootdn_pwd => $rootdn_pwd,
            config_directory_ldap_url => $config_directory_ldap_url,
            config_directory_admin_id => $config_directory_admin_id,
            config_directory_admin_pwd => $config_directory_admin_pwd,
            admin_bind_ip => $admin_bind_ip,
            admin_port => $admin_port,
            server_admin_id => $server_admin_id,
            server_admin_pwd => $server_admin_pwd,
        }
    }

    include dirsrv::service

    if tagged('monit') {
        class { 'dirsrv::monit':
            serveridentifier => $serveridentifier,
            monitor_email => $monitor_email,
        }
    }

    if tagged('packetfilter') {

        include ldap

        class { 'ldap::packetfilter':
            allow_ipv4_address => $dirsrv_allow_ipv4_address,
            allow_ipv6_address => $dirsrv_allow_ipv6_address,
            allow_ports => $ldap_port,
        }

        class { 'dirsrv::packetfilter::admin':
            allow_ipv4_address => $admin_srv_allow_ipv4_address,
            allow_ipv6_address => $admin_srv_allow_ipv6_address,
            port => $admin_port,
        }
    }
}
}
